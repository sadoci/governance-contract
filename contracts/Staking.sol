pragma solidity ^0.4.24;

import "openzeppelin-solidity/contracts/math/SafeMath.sol";
import "openzeppelin-solidity/contracts/utils/ReentrancyGuard.sol";
import "./GovChecker.sol";


contract Staking is GovChecker, ReentrancyGuard {
    using SafeMath for uint256;

    mapping(address => uint256) private _balance;
    mapping(address => uint256) private _lockedBalance;
    uint256 private _totalLockedBalance;
    
    event Staked(address indexed payee, uint256 amount, uint256 total, uint256 available);
    event Unstaked(address indexed payee, uint256 amount, uint256 total, uint256 available);
    event Locked(address indexed payee, uint256 amount, uint256 total, uint256 available);
    event Unlocked(address indexed payee, uint256 amount, uint256 total, uint256 available);

    constructor(address _registry) public {
        _totalLockedBalance = 0;
        setRegistry(_registry);
    }

    function balanceOf(address payee) public view returns (uint256) {
        return _balance[payee];
    }

    function lockedBalanceOf(address payee) public view returns (uint256) {
        return _lockedBalance[payee];
    }

    function availableBalance(address payee) public view returns (uint256) {
        return _balance[payee].sub(_lockedBalance[payee]);
    }

    function calcVotingWeight(address payee) public view returns (uint256) {
        if (_lockedBalance[payee] == 0) return 0;
        return _lockedBalance[payee].mul(100).div(_totalLockedBalance);
    }

    function () external payable {
        deposit();
    }

    /**
    * @dev Deposit from a sender.
    */
    function deposit() public nonReentrant payable {
        require(msg.value > 0, "Deposit amount should be greater than zero");

        _balance[msg.sender] = _balance[msg.sender].add(msg.value);

        emit Staked(msg.sender, msg.value, _balance[msg.sender], availableBalance(msg.sender));
    }

    /**
    * @dev Withdraw for a sender.
    * @param amount The amount of funds will be withdrawn and transferred to.
    */
    function withdraw(uint256 amount) external nonReentrant {
        require(amount <= availableBalance(msg.sender), "Withdraw amount should be equal or less than balance");

        _balance[msg.sender] = _balance[msg.sender].sub(amount);
        msg.sender.transfer(amount);

        emit Unstaked(msg.sender, amount, _balance[msg.sender], availableBalance(msg.sender));
    }

    /**
    * @dev Lock fund
    * @param payee The address whose funds will be locked.
    * @param lockAmount The amount of funds will be locked.
    */
    function lock(address payee, uint256 lockAmount) external onlyGov {
        require(_balance[payee] >= lockAmount, "Lock amount should be less than balance");
        require(_balance[payee].sub(_lockedBalance[payee]) >= lockAmount, "Insufficient lockable balance");

        _lockedBalance[payee] = _lockedBalance[payee].add(lockAmount);
        _totalLockedBalance = _totalLockedBalance.add(lockAmount);

        emit Locked(payee, lockAmount, _balance[payee], availableBalance(payee));
    }

    /**
    * @dev Unlock fund
    * @param payee The address whose funds will be unlocked.
    * @param unlockAmount The amount of funds will be unlocked.
    */
    function unlock(address payee, uint256 unlockAmount) external onlyGov {
        require(_lockedBalance[payee] >= unlockAmount, "Unlock amount should be less than locked");

        _lockedBalance[payee] = _lockedBalance[payee].sub(unlockAmount);
        _totalLockedBalance = _totalLockedBalance.sub(unlockAmount);

        emit Unlocked(payee, unlockAmount, _balance[payee], availableBalance(payee));
    }

}